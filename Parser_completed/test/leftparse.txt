01) <Prog> ::= KW_PROGRAM TK_IDENT SB_SEMICOLON <Block> SB_PERIOD
02) <Block> ::= KW_CONST <ConstDecl> <ConstDecls> <Block2>
03) <Block> ::= <Block2>
04) <Block2> ::= KW_TYPE <TypeDecl> <TypeDecls> <Block3>
05) <Block2> ::= <Block3>
06) <Block3> ::= KW_VAR <VarDecl> <VarDecls><Block4>
07) <Block3> ::= <Block4>
08) <Block4> ::= <SubDecls><Block5>
09) <Block4> ::=|<Block5>
10) <Block5> ::= KW_BEGIN <Statements> KW_END
11) <ConstDecls>::= <ConstDecl> <ConstDecls>
12) <ConstDecls>::= 
13) <ConstDecl> ::= TK_IDENT SB_EQUAL <Constant> SB_SEMICOLON
14) <TypeDecls> ::= <TypeDecl> <TypeDecls>
15) <TypeDecls> ::= 
16) <TypeDecl> ::= TK_IDENT SB_EQUAL <Type> SB_SEMICOLON
17) <VarDecls>::= <VarDecl> <VarDecls>
18) <VarDecls>::= 
19) <VarDecl> ::= TK_IDENT SB_COLON <Type> SB_SEMICOLON
20) <SubDecls> ::= <FunDecl> <SubDecls>
21) <SubDecls> ::= <ProcDecl> <SubDecls>
22) <SubDecls> ::= 
23) <FunDecl> ::= KW_FUNCTION TK_IDENT <Params> SB_COLON <BasicType> 
SB_SEMICOLON 
<Block> SB_SEMICOLON
24) <ProcDecl> ::= KW_PROCEDURE TK_IDENT <Params> SB_SEMICOLON <Block> 
SB_SEMICOLON
25) <Params> ::= SB_LPAR <Param> <Params2> SB_RPAR
26) <Params> ::= 
27) <Params2> ::= SB_SEMICOLON <Param> <Params2>
28) <Params2> ::= 
29) <Param> ::= TK_IDENT SB_COLON <BasicType> 
30) <Param> ::= KW_VAR TK_IDENT SB_COLON <BasicType> 
31) <Type> ::= KW_INTEGER 
32) <Type> ::= KW_CHAR 
33) <Type> ::= TK_IDENT 
34) <Type> ::= KW_ARRAY SB_LSEL TK_NUMBER SB_RSEL KW_OF <Type>
35) <BasicType> ::= KW_INTEGER 
36) <BasicType> ::= KW_CHAR
37) <UnsignedConstant> ::= TK_NUMBER
38) <UnsignedConstant> ::= TK_IDENT
39) <UnsignedConstant> ::= TK_CHAR
40) <Constant> ::= SB_PLUS <Constant2>
41) <Constant> ::= SB_MINUS <Constant2> 
42) <Constant> ::= <Constant2> 
43) <Constant> ::= TK_CHAR
44) <Constant2>::= TK_IDENT
45) <Constant2>::= TK_NUMBER
46) <Statements> ::= <Statement> <Statements2>
47) <Statements2> ::= SB_SEMICOLON <Statement> <Statements2>
48) <Statements2> ::= 
49) <Statement> ::= <AssignSt>
50) <Statement> ::= <CallSt>
51) <Statement> ::= <GroupSt>
52) <Statement> ::= <IfSt>
53) <Statement> ::= <WhileSt>
54) <Statement> ::= <ForSt>
55) <Statement> ::= 
56) <AssignSt> ::= <Variable> SB_ASSIGN <Expression>
57) <AssignSt> ::= TK_IDENT SB_ASSIGN <Expression>
58) <CallSt> ::= KW_CALL TK_IDENT <Arguments>
59) <GroupSt> ::= KW_BEGIN <Statements> KW_END
60) <IfSt> ::= KW_IF <Condition> KW_THEN <Statement> <ElseSt>
61) <ElseSt> ::= KW_ELSE <Statement>
62) <ElseSt> ::= 
63) <WhileSt> ::= KW_WHILE <Condition> KW_DO <Statement>
64) <ForSt> ::= KW_FOR TK_IDENT SB_ASSIGN <Expression> KW_TO 
<Expression> KW_DO <Statement>
65) <Arguments> ::= SB_LPAR <Expression> <Arguments2> SB_RPAR
66) <Arguments> ::= 
67) <Arguments2>::= SB_COMMA <Expression> <Arguments2>
68) <Arguments2>::= 
69) <Condition> ::= <Expression> <Condition2>
70) <Condition2>::= SB_EQ <Expression>
71) <Condition2>::= SB_NEQ <Expression>
72) <Condition2>::= SB_LE <Expression>
73) <Condition2>::= SB_LT <Expression>
74) <Condition2>::= SB_GE <Expression>
75) <Condition2>::= SB_GT <Expression>
76) <Expression> ::= SB_PLUS <Expression2>
77) <Expression> ::= SB_MINUS <Expression2>
78) <Expression> ::= <Expression2>
79) <Expression2> ::= <Term> <Expression3>
80) <Expression3> ::= SB_PLUS <Term> <Expression3>
81) <Expression3> ::= SB_MINUS <Term> <Expression3>
82) <Expression3> ::= 
83) <Term> ::= <Factor> <Term2>
84) <Term2> ::= SB_TIMES <Factor> <Term2>
85) <Term2> ::= SB_SLASH <Factor> <Term2>
86) <Term2> ::= 
87) <Factor> ::= <UnsignedConstant>
88) <Factor> ::= <Variable>
89) <Factor> ::= <FunctionApptication>
90) <Factor> ::= SB_LPAR <Expression> SB_RPAR
91) <Variable> ::= TK_IDENT <Indexes>
92) <FunctionApplication> ::= TK_IDENT <Arguments>
93) <Indexes> ::= SB_LSEL <Expression> SB_RSEL <Indexes>
94) <Indexes> ::= 

